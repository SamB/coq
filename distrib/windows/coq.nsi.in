; This script is used to build the Windows install program for Coq.

;NSIS Modern User Interface
;Written by Joost Verburg
;Modified by Julien Narboux

; This file is preprocessed by ./configure to subtitute @VERSION@ by the current version.

!define MY_PRODUCT "Coq" ;Define your own software name here
!define MY_VERSION "@VERSION@" ;Define your own software version here
!define COQ_EXE_NAME "coq.exe"
!define COQ_EXE_PATH "..\..\bin\coq.exe"
!define COQIDE_EXE_NAME "coqide.exe"
!define COQIDE_EXE_PATH "..\..\bin\coqide.exe"

!include "MUI.nsh"

;--------------------------------
;Configuration

  Name "Coq"

  ;General
  OutFile "Coq_@VERSION@_win32_installer.exe"

  ;Folder selection page
  InstallDir "$PROGRAMFILES\${MY_PRODUCT}"
  
  ;Remember install folder
  InstallDirRegKey HKCU "Software\${MY_PRODUCT}" ""

;Interface Configuration

;  !define MUI_HEADERIMAGE
;  !define MUI_HEADERIMAGE_BITMAP "coq_logo.bmp" ; optional
;  !define MUI_ABORTWARNING


;--------------------------------
;Modern UI Configuration

  !insertmacro MUI_PAGE_WELCOME
  !insertmacro MUI_PAGE_LICENSE "..\..\COPYING"
  !insertmacro MUI_PAGE_COMPONENTS
  !insertmacro MUI_PAGE_DIRECTORY
  !insertmacro MUI_PAGE_INSTFILES
  !insertmacro MUI_PAGE_FINISH
  
  !insertmacro MUI_UNPAGE_WELCOME
  !insertmacro MUI_UNPAGE_CONFIRM
  !insertmacro MUI_UNPAGE_INSTFILES
  !insertmacro MUI_UNPAGE_FINISH  

;--------------------------------
;Languages
 
  !insertmacro MUI_LANGUAGE "English"
  
;--------------------------------
;Language Strings

  ;Description
  LangString DESC_1 ${LANG_ENGLISH} "This is the windows version of Coq."
  LangString DESC_2 ${LANG_ENGLISH} "This is CoqIde, an interactive development environment for Coq."
  LangString DESC_3 ${LANG_ENGLISH} "This will copy the GTK dlls in the installation directory (These files are needed by CoqIde)."

;--------------------------------
;Data
  
;Function .onInit
;  SetOutPath $TEMP
;  File /oname=coq_splash.bmp "coq_splash.bmp"
;	InitPluginsDir;

;  advsplash::show 1000 600 400 -1 $TEMP\coq_splash

;  Pop $0 ; $0 has '1' if the user closed the splash screen early,
;         ; '0' if everything closed normal, and '-1' if some error occured.

;  Delete $TEMP\coq_splash.bmp
;FunctionEnd


;--------------------------------
;Installer Sections

Section "Coq" Sec1

  ;ADD YOUR OWN STUFF HERE!

  SetOutPath "$INSTDIR"
  File "${COQ_EXE_PATH}"
  File "coq.ico"

  ;Store install folder
  WriteRegStr HKCU "Software\${MY_PRODUCT}" "" $INSTDIR
  
  ;Create uninstaller
  WriteUninstaller "$INSTDIR\Uninstall.exe"
  WriteRegStr HKEY_LOCAL_MACHINE "Software\Microsoft\Windows\CurrentVersion\Uninstall\Coq" \
	  "DisplayName" "Coq Version ${MY_VERSION}"
  WriteRegStr HKEY_LOCAL_MACHINE "Software\Microsoft\Windows\CurrentVersion\Uninstall\Coq" \
	  "UninstallString" '"$INSTDIR\Uninstall.exe"'

  WriteRegStr HKEY_LOCAL_MACHINE "Software\Microsoft\Windows\CurrentVersion\Uninstall\Coq" \
          "DisplayVersion" "${MY_VERSION}"

  WriteRegDWORD HKEY_LOCAL_MACHINE "Software\Microsoft\Windows\CurrentVersion\Uninstall\Coq" \
          "NoModify" "1"
  WriteRegDWORD HKEY_LOCAL_MACHINE "Software\Microsoft\Windows\CurrentVersion\Uninstall\Coq" \
          "NoRepair" "1"

  WriteRegStr HKEY_LOCAL_MACHINE "Software\Microsoft\Windows\CurrentVersion\Uninstall\Coq" \
          "URLInfoAbout" "http://coq.inria.fr"

; Start Menu Entries
  CreateDirectory "$SMPROGRAMS\Coq"
  CreateShortCut "$SMPROGRAMS\Coq\Coq.lnk" "$INSTDIR\coq.exe" "" "$INSTDIR\coq.ico" 0 
  WriteINIStr "$SMPROGRAMS\Coq\Coq-HomePage.url" "InternetShortcut" "URL" "http://coq.inria.fr"
  CreateShortCut "$SMPROGRAMS\Coq\Uninstall.lnk" "$INSTDIR\Uninstall.exe" "" "$INSTDIR\Uninstall.exe" 0

SectionEnd

Section "CoqIde" Sec2

  SetOutPath "$INSTDIR"
  File "${COQIDE_EXE_PATH}"

  ; Start Menu Entries
  CreateShortCut "$SMPROGRAMS\Coq\CoqIde.lnk" "$INSTDIR\${COQIDE_EXE_NAME}" "" "$INSTDIR\coq.ico" 0 

SectionEnd

Section  "The GTK DLLs (needed by CoqIde)" Sec3
  
  SetOutPath "$INSTDIR"
  File /r /x CVS  dlls\*.*

SectionEnd

;--------------------------------
;Descriptions

!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${Sec1} $(DESC_1)
  !insertmacro MUI_DESCRIPTION_TEXT ${Sec2} $(DESC_2)
  !insertmacro MUI_DESCRIPTION_TEXT ${Sec3} $(DESC_3)
!insertmacro MUI_FUNCTION_DESCRIPTION_END
 
;--------------------------------
;Uninstaller Section

Section "Uninstall"

;; Binaries
  Delete "$INSTDIR\${COQ_EXE_NAME}"
  Delete "$INSTDIR\${COQIDE_EXE_NAME}"

;; Icon
  Delete "$INSTDIR\coq.ico"

;; DLLs

  Delete "$INSTDIR\iconv.dll" 
  Delete "$INSTDIR\libgdk-win32-2.0-0.dll"
  Delete "$INSTDIR\libgdk_pixbuf-2.0-0.dll"
  Delete "$INSTDIR\libglib-2.0-0.dll"
  Delete "$INSTDIR\libgobject-2.0-0.dll"
  Delete "$INSTDIR\libgtk-win32-2.0-0.dll"
  Delete "$INSTDIR\libpango-1.0-0.dll"
  Delete "$INSTDIR\librsvg-2-2.dll"
  Delete "$INSTDIR\freetype6.dll"
  Delete "$INSTDIR\iconv.dll"
  Delete "$INSTDIR\intl.dll"
  Delete "$INSTDIR\libart_lgpl_2-2.dll"
  Delete "$INSTDIR\libatk-1.0-0.dll"
  Delete "$INSTDIR\libfontconfig-1.dll"
  Delete "$INSTDIR\libgdk_pixbuf-2.0-0.dll"
  Delete "$INSTDIR\libgmodule-2.0-0.dll"
  Delete "$INSTDIR\libgsf-1-1.dll"
  Delete "$INSTDIR\libpangoft2-1.0-0.dll"
  Delete "$INSTDIR\libpangowin32-1.0-0.dll"
  Delete "$INSTDIR\libxml2.dll"
  Delete "$INSTDIR\xmlparse.dll"
  Delete "$INSTDIR\xmltok.dll"
  Delete "$INSTDIR\zlib1.dll"
  RMDir /r "$INSTDIR\etc"
  RMDir /r "$INSTDIR\lib"
    
;; Start Menu
  Delete "$SMPROGRAMS\Coq\Coq.lnk"
  Delete "$SMPROGRAMS\Coq\CoqIde.lnk"
  Delete "$SMPROGRAMS\Coq\Uninstall.lnk"
  Delete "$SMPROGRAMS\Coq\Coq-HomePage.url"
  Delete "$INSTDIR\Uninstall.exe"
  
  DeleteRegKey /ifempty HKCU "Software\${MY_PRODUCT}"

  DeleteRegKey HKEY_LOCAL_MACHINE "SOFTWARE\Coq"
  DeleteRegKey HKEY_LOCAL_MACHINE "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\Coq"
  RMDir "$INSTDIR"
  RMDir "$SMPROGRAMS\Coq"

SectionEnd